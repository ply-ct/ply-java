plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'org.ply-ct'
version '1.0.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Plyex'
                description = 'OpenAPI augmentation based on Ply autotests'
                packaging = 'jar'
                url = 'https://ply-ct.org'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'oakesville'
                        name = 'Donald Oakes'
                        email = 'donald@oakesville.io'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ply-ct/ply-java.git'
                    developerConnection = 'scm:git:https://github.com/ply-ct/ply-java.git'
                    url = 'https://github.com/ply-ct/ply-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : System.getenv("MAVEN_USERNAME")
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : System.getenv("MAVEN_PASSWORD")
            }
        }
        mavenLocal()
    }
}

signing {
    sign publishing.publications.mavenJava
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.testing.compile:compile-testing:0.11', {
        exclude group: 'com.sun', module: 'tools'
    }
    implementation 'com.beust:jcommander:1.82'
    implementation 'org.yaml:snakeyaml:1.33'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.github.jknack:handlebars:4.3.1'
    implementation 'org.springframework:spring-web:5.3.24'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

test {
    useJUnitPlatform()
}